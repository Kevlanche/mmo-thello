#include mmothello/[
  ClientToServer,
  ServerToClient,
  Board
]

struct Click: int x, int y;

binding ClientBinding {
  Click? getClick();
  void? postToServer(ClientToServer msg);
  ServerToClient? getIncomingMessage();
  void? setBoard(int x, int y, byte color);
  void? setLegality(int x, int y, bool isLegal);

  byte? getLocalId();
}

worker Client {
  ClientBinding~ bind;
  Board~ lastBoard;
  Board~ legalityState;

  new(ClientBinding~ bind)
    lastBoard: new()
    , legalityState: new();

  void? handleIncomingMessages~() {
    while (true) {
      $msg = bind.getIncomingMessage() ?: return;
      trace "My local id is: $(bind.getLocalId() ?: 0b)";
      if ($board = msg.refreshBoard) {

        $anyChanges = false;
        for ($x = 0; < 16) {
          for ($y = 0; < 16) {
            $known = lastBoard.get(x, y);
            $incoming = board.get(x, y);
            if (known != incoming) {
              bind.setBoard(x, y, incoming);
              lastBoard.set(x, y, incoming);
              anyChanges = true;
            }
          }
        }
        if (anyChanges) {
          if ($myId = bind.getLocalId()) {
            for ($x = 0; < 16) {
              for ($y = 0; < 16) {
                $isLegal = lastBoard.isLegalMove(x, y, myId);
                $wasLegal = legalityState.get(x, y) != 0b;
                if (isLegal != wasLegal) {
                  bind.setLegality(x, y, isLegal);

                  if (isLegal): legalityState.set(x, y, 1b);
                  else: legalityState.set(x, y, 0b);
                }
              }
            }
          }
        }
      } else {
        trace "ERR unknown message type";
      }
    }
  }

  void? update~() {
    handleIncomingMessages();

    if ($cl = bind.getClick()) {
      trace "Got click @ $(cl.x) | $(cl.y)";
      bind.postToServer(.newOnClick(new(cl.x, cl.y)));
    }
  }

  static .void()? game(ClientBinding~ bind) {
    trace "Client started";
    Client~ cl = new(bind);
    return () -> {
      cl.update();
    };
  }
}
